// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:   
   Not16(in=y,out=ybar);
   Mux16(a=x,b=false,sel=zx,out=o1);
   Not16(in=o1,out=o1bar);
   Mux16(a=o1,b=o1bar,sel=nx,out=o3);
   Mux16(a=y,b=false,sel=zy,out=o2);
   Not16(in=o2,out=o2bar);
   Mux16(a=o2,b=o2bar,sel=ny,out=o4);
   Add16(a=o3,b=o4,out=o5);
   And16(a=o3,b=o4,out=o6);
   Mux16(a=o6,b=o5,sel=f,out=w1);
   Not16(in=w1,out=w1bar);
   Mux16(a=w1,b=w1bar,sel=no,out=out,out[15]=ng,out[0..7]=n1,out[8..15]=n2);
   //Not(in=oo[15],out=ng1);
   //Not(in=ng1,out=ng);
   Or8Way(in=n1,out=p1);
   Or8Way(in=n2,out=p2);
   Or(a=p1,b=p2,out=p);
   Not(in=p,out=zr);
   //Or16(a=oo,b[0..15]=false,out=out);

}